// Copyright 2018, OpenNext SAS
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
// Alert:  haproxy_backend_alert
// Define: kapacitor define haproxy_backend_error_alert_stream -type stream \
//           -tick haproxy_backend_error_alert_stream.tick -dbrp telegraf.autogen
// Enable: kapacitor enable haproxy_backend_state_alert_stream


// Parameters
var period = 1m
var every = 1m
var unit = 10s

var rawdata = batch
  |query('''SELECT econ, dresp, eresp FROM "telegraf"."autogen"."haproxy" WHERE type = 'server' ''')
    .period(period)
    .every(every)
    .groupBy('region', 'host', 'proxy', 'sv')

var econ = rawdata
  |derivative('econ')
    .as('value')
    .nonNegative()
    .unit(unit)
    
var dresp = rawdata
  |derivative('dresp')
    .as('value')
    .nonNegative()
    .unit(unit)

var eresp = rawdata
  |derivative('eresp')
    .as('value')
    .nonNegative()
    .unit(unit)

var data = econ 
  |join(dresp, eresp)
    .as('econ', 'dresp', 'eresp')

// Thresholds
var alert = data
  |alert()
    .id('haproxy-{{ index .Tags "proxy" }}-errors-alert')
    .message('{{ .ID }}: [{{ .Level }}] {{ index .Tags "sv" }}: HAProxy errors. Rate of change for econ={{ index .Fields "econ.value" }} dresp={{ index .Fields "dresp.value" }} eresp={{ index .Fields "eresp.value" }}')
    .warn(lambda: "econ.value" > 0 OR "dresp.value" > 0 OR "eresp.value" > 0)
    .topic('haproxy')
//  .log('/var/log/kapacitor/haproxy_backend_error_alert.log')
