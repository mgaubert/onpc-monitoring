// Copyright 2018, OpenNext SAS
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

// Parameters
var info = 100.0
var warn = 75.0
var crit = 50.0

// Dataframe
var rawdata = stream
  |from()
    .database('telegraf')
    .retentionPolicy('autogen')
    .measurement('ceph')
    .groupBy('region')

var total = rawdata
  |last('num_mon')
    .as('value')

var quorum = rawdata
  |last('num_mon_quorum')
    .as('value')

var data = total
  |join(quorum)
    .as('total', 'quorum')

// Thresholds
var alert = data 
  |eval(lambda: float("quorum.value") / float("total.value") * 100.0)
    .as('stat')
  |alert()
    .id('{{ index .Tags "environment" }}/{{ index .Tags "region" }}/ceph/mon/cluster_status')
    .message('{{ .ID }}: [{{ .Level }}] Availability state of Ceph monitor cluster is {{ index .Fields "stat" | printf "%0.2f" }}%')
    .info(lambda: "stat" <= info)
    .warn(lambda: "stat" <= warn)
    .crit(lambda: "stat" <= crit)
    .topic('ceph')
//  .log('/var/log/kapacitor/ceph_mon_cluster_alert.log')
    
    