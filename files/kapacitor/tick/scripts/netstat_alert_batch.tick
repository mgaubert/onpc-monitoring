// netstat_alert_batch

// metric: 'tcp_established'
// available_fields:  "tcp_close","tcp_close_wait","tcp_closing","tcp_established","tcp_fin_wait1","tcp_fin_wait2","tcp_last_ack","tcp_listen","tcp_none","tcp_syn_recv","cp_syn_sent","tcp_time_wait","udp_socket","tcp_listen"

// TELEGRAF CONFIGURATION
// [[inputs.netstat]]

// DEFINE: kapacitor define netstat_alert_batch -type stream -tick netstat/netstat_alert_batch.tick -dbrp telegraf.autogen
// ENABLE: kapacitor enable netstat_alert_batch

// Parameters

var info = 10
var warn = 30
var crit = 90
var period = 1m 
var every = 1m 
var unit = 1s

var rawdata = batch
  |query('''SELECT tcp_established, tcp_time_wait, tcp_close_wait FROM "telegraf"."autogen"."netstat" ''')
    .period(period)
    .every(every)
    .groupBy('region', 'host')

var tcp_established = rawdata
  |derivative('tcp_established')
    .as('value')
    .nonNegative()
    .unit(unit)

var tcp_time_wait = rawdata
  |derivative('tcp_time_wait')
    .as('value')
    .nonNegative()
    .unit(unit)

var tcp_close_wait = rawdata
  |derivative('tcp_close_wait')
    .as('value')
    .nonNegative()
    .unit(unit)

var data = tcp_close_wait
  |join(tcp_time_wait, tcp_established)
    .as('tcp_close_wait', 'tcp_time_wait', 'tcp_established')

var alert = data
  |alert()
//    .id('{{ index .Tags "environment" }}/{{ index .Tags "region" }}/{{ index .Tags "host" }}/netstat')
    .id('netstat-alert')
    .message('{{ .ID }}: [{{ .Level }}] {{ index .Tags "host" }}: tcp_established={{ index .Fields "tcp_established.value" }} tcp_time_wait={{ index .Fields "tcp_time_wait.value" }} tcp_close_wait={{ index .Fields "tcp_close_wait.value" }} derivatives')
    .info(lambda: "tcp_established.value" >= info OR "tcp_time_wait.value" >= info OR "tcp_close_wait.value" >= info)
    .warn(lambda: "tcp_established.value" >= warn OR "tcp_time_wait.value" >= warn OR "tcp_close_wait.value" >= warn)
    .crit(lambda: "tcp_established.value" >= crit OR "tcp_time_wait.value" >= crit OR "tcp_close_wait.value" >= crit)
    .topic('network')
//  .log('/var/log/kapacitor/netstat_alert_batch.log')
